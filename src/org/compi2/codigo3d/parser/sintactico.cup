package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER, IGUAL, DIFERENTE, MAYOR, MENOR, MAYOR_IG, MENOR_IG, AND, OR, NOT;
nonterminal NodoC3D inicial, expresion, condicional, relacional;
nonterminal String oprelacional;
precedence left OR;
precedence left AND;
precedence left NOT;
precedence left MAYOR, MENOR, MAYOR_IG, MENOR_IG, IGUAL, DIFERENTE;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} condicional:exp 
                    {: 
                        
                        
                        if(exp.getETQV()!=""){
                        String c3d = "//Etiqueta(s) de verdadero: "+exp.getETQV()+"\n"+"//Etiqueta(s) de falso: "+exp.getETQF()+"\n";
                        ControlC3D.agregarC3D(c3d);
                        //System.out.println("//Etiqueta(s) de verdadero: "+exp.getETQV()+"\n"+"//Etiqueta(s) de falso: "+exp.getETQF()+"\n");
                        } else {
                        String c3d = "//Ultimo valor: " + exp.getCad();
                        ControlC3D.agregarC3D(c3d);
                        //System.out.println("//Ultimo valor: " + exp.getCad());
                        }
                        RESULT = exp;
                       
                    :};
condicional   ::= condicional:izq AND {:String c3d = izq.getETQV() + ":\n"; ControlC3D.agregarC3D(c3d);:} condicional:der
                    {:                     
                        String etqV = der.getETQV();
                        String etqF = izq.getETQF() + "," + der.getETQF();
                        String c3d = "//Etiqueta(s) de verdadero: "+etqV+"\n"+"//Etiqueta(s) de falso: "+etqF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(etqV,etqF);
                    :}
              | condicional:izq OR{:String c3d = izq.getETQF() + ":\n"; ControlC3D.agregarC3D(c3d); :}condicional:der
                    {:
                        String etqV = izq.getETQV() + "," + der.getETQV();
                        String etqF = der.getETQF();
                        String c3d = "//Etiqueta(s) de verdadero: "+etqV+"\n"+"//Etiqueta(s) de falso: "+etqF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(etqV,etqF);
                        
                    :}
              | NOT condicional:unico
                    {:
                        String etqV = unico.getETQF();
                        String etqF = unico.getETQV();
                        String c3d = "//Etiqueta(s) de verdadero: "+ etqV +"\n"+"//Etiqueta(s) de falso: "+ etqF +"\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(etqV,etqF);

                    :}
                    |relacional:rel
                        {:
                            RESULT = rel;
                        :}
              ;

relacional          ::=expresion:izq oprelacional:opr expresion:der
                    {:
                        String etqV = ControlC3D.generaEtiqueta();
                        String etqF = ControlC3D.generaEtiqueta();
                        String c3d = "if ("+izq.getCad() + opr + der.getCad() +") goto " +etqV+ ";\n" +"goto "+etqF+"\n";
                        ControlC3D.agregarC3D(c3d);
                        System.out.println("if ("+izq.getCad() + opr + der.getCad() +") goto " +etqV+ ";\n" +"goto "+etqF+"\n");
                        RESULT = new NodoC3D(etqV,etqF);
                    :}
                    | expresion:expresion
                           {:
                               RESULT = expresion;
                           :}
                    ;

oprelacional   ::= IGUAL
                    {:
                        
                        RESULT = "==";
                    :}
            |   DIFERENTE
                    {:
                        RESULT = "!=";
                    :}
            |   MAYOR
                    {:
                        RESULT = ">";
                    :}
            |   MENOR
                    {:
                        RESULT = "<";
                    :}
            |   MAYOR_IG
                    {:
                        RESULT = ">=";
                    :}
            |   MENOR_IG 
                    {:
                        RESULT = "<=";
                    :}
            ;
expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            | PAR_IZQ condicional:exp PAR_DER 
                    {: 
                    RESULT = exp; 
                    :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;